group 'de.honoka.sdk'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group rootProject.group
    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    dependencies {
        String lombok = 'org.projectlombok:lombok:1.18.16'
        compileOnly lombok
        annotationProcessor lombok
        testCompileOnly lombok
        testAnnotationProcessor lombok
        //Test
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        repositories {
            mavenLocal()
            if(project.hasProperty('remoteMavenRepositoryUrl')) {
                maven {
                    url remoteMavenRepositoryUrl
                }
            }
        }
    }

    ext {
        setupVersion = { String versionStr ->
            version versionStr

            publishing {
                publications {
                    maven(MavenPublication) {
                        groupId project.group
                        artifactId project.name
                        version versionStr

                        from components.java

                        artifact sourcesJar
                    }
                }
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
